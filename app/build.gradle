apply plugin: 'com.android.application'

dependencies {
    compile fileTree(dir: 'libs', include: ['*.jar'])
    compile 'com.android.support:support-v4:21.0.3'
    compile 'com.squareup.picasso:picasso:2.4.0'
}

android {
    compileSdkVersion 21
    buildToolsVersion "21.1.2"

    productFlavors {
        live {
        }

        staging {
            // You can have a different package name for a staging build, so both flavors can be
            // installed side-by-side.
            // You will need to substitute the package name for ${applicationId} in
            // AndroidManifest.xml for Content Providers etc.
            applicationId "com.monitisecreate.myapp.staging"
        }
    }

    // Set versionCode and versionName based off Jenkins build number
    def suffix = System.getenv("BUILD_NUMBER")

    defaultConfig {
        applicationId "com.monitisecreate.myapp"
        minSdkVersion 16
        targetSdkVersion 21
        versionCode 1
        versionName "1.0.0"

        if (suffix != null) {
            def vers = Integer.parseInt(suffix)
            versionCode vers
        }
    }

    signingConfigs {
        debug {
            // Having a shared debug keystore means that the app only needs to be configured for
            // APIs like Google Maps, Facebook etc with one key hash
            storeFile rootProject.file("config/signing/debug.keystore")
        }

        release {
            storeFile rootProject.file("config/signing/release.keystore")
            storePassword 'skeleton'
            keyAlias 'skeleton'
            keyPassword 'skeleton'
        }
    }

    buildTypes {
        debug {
            signingConfig signingConfigs.debug
            buildConfigField "String", "TOAST_STRING", "\"it's debug\""
            buildConfigField "boolean", "TOAST_BOOLEAN", "true"
        }

        release {
            signingConfig signingConfigs.release
            buildConfigField "String", "TOAST_STRING", "\"it's release\""
            buildConfigField "boolean", "TOAST_BOOLEAN", "false"

            // Release builds add the Jenkins BUILD_NUMBER as a suffix to the version name
            // This is the preferred versioning scheme (i.e. 1.0.0.<BUILD_NUMBER>)
            if (suffix != null) {
                versionNameSuffix "." + suffix
            }

            minifyEnabled true
            proguardFiles getDefaultProguardFile('proguard-android.txt'),
                    // Project-specific Proguard file
                    rootProject.file('config/proguard/proguard-rules.pro'),
                    // Proguard for commonly-used libraries (see config/proguard/libraries for more)
                    // Derived from: https://github.com/krschultz/android-proguard-snippets
                    rootProject.file('config/proguard/libraries/proguard-gson.pro'),
                    rootProject.file('config/proguard/libraries/proguard-square-picasso.pro')
        }
    }

    apply from: rootProject.file('config/checkstyle/checkstyle.gradle')
    apply from: rootProject.file('config/findbugs/findbugs.gradle')
}
